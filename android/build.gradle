buildscript {
    // Buildscript is evaluated before everything else so we can't use getExtOrDefault
    def kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['HyperSdkReact_kotlinVersion']
    def useDynamicAssets = rootProject.ext.has('useDynamicAssets') ? rootProject.ext.get('useDynamicAssets') : false

    repositories {
        google()
        jcenter()

        if (useDynamicAssets) {
            maven {
                url "https://maven.juspay.in/jp-build-packages/hypersdk-asset-download/releases/"
            }
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        // noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        if (useDynamicAssets) {
            classpath 'in.juspay:hypersdk-asset-plugin:1.0.3'
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
if (rootProject.hasProperty('useDynamicAssets') && rootProject.useDynamicAssets) {
    apply plugin: 'hypersdk-asset-plugin'
}

def getExtOrDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['HyperSdkReact_' + name]
}

def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['HyperSdkReact_' + name]).toInteger()
}

android {
    compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')
    buildToolsVersion getExtOrDefault('buildToolsVersion')
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()

    def found = false
    def defaultDir = null
    def androidSourcesName = 'React Native sources'

    if (rootProject.ext.has('reactNativeAndroidRoot')) {
        defaultDir = rootProject.ext.get('reactNativeAndroidRoot')
    } else {
        defaultDir = new File(
            projectDir,
            '/../../../node_modules/react-native/android'
        )
    }

    if (defaultDir.exists()) {
        maven {
            url defaultDir.toString()
            name androidSourcesName
        }

        logger.info(":${project.name}:reactNativeAndroidRoot ${defaultDir.canonicalPath}")
        found = true
    } else {
        def parentDir = rootProject.projectDir

        1.upto(5, {
            if (found) return true
            parentDir = parentDir.parentFile

            def androidSourcesDir = new File(
                parentDir,
                'node_modules/react-native'
            )

            def androidPrebuiltBinaryDir = new File(
                parentDir,
                'node_modules/react-native/android'
            )

            if (androidPrebuiltBinaryDir.exists()) {
                maven {
                    url androidPrebuiltBinaryDir.toString()
                    name androidSourcesName
                }

                logger.info(":${project.name}:reactNativeAndroidRoot ${androidPrebuiltBinaryDir.canonicalPath}")
                found = true
            } else if (androidSourcesDir.exists()) {
                maven {
                    url androidSourcesDir.toString()
                    name androidSourcesName
                }

                logger.info(":${project.name}:reactNativeAndroidRoot ${androidSourcesDir.canonicalPath}")
                found = true
            }
        })
    }

    if (!found) {
        throw new GradleException(
            "${project.name}: unable to locate React Native android sources. " +
                "Ensure you have you installed React Native as a dependency in your project and try again."
        )
    }
}

static def mostRecentVersion(List<String> versions) {
    return versions.sort(false) { a, b ->
        [a, b]*.tokenize('-rc')*.join('')*.tokenize('.')*.collect {
            it as int
        }.with { u, v ->
            [u, v].transpose().findResult { x, y -> x <=> y ?: null } ?: u.size() <=> v.size()
        }
    } [-1]
}

def getHyperSDKVersion() {
    def hyper_sdk_version = '2.1.2-rc.01'
    if (rootProject.hasProperty('useDynamicAssets') && rootProject.useDynamicAssets) {
        hyper_sdk_version = '2.1.7'
    }
    if (rootProject.hasProperty('hyperSDKVersion')) {
        def rootVersion = rootProject.hyperSDKVersion
        hyper_sdk_version = mostRecentVersion([hyper_sdk_version, rootVersion])
        if (hyper_sdk_version != rootVersion) {
            System.out.println("Ignoring the overriden SDK version present in root build.gradle ($rootVersion), as there is a newer version present in the SDK ($hyper_sdk_version).")
        }
    }
    System.out.println("HyperSDK Version: $hyper_sdk_version")
    return hyper_sdk_version
}

dependencies {
    // noinspection GradleDynamicVersion
    api 'com.facebook.react:react-native:+'
    //noinspection GradleDependency
    implementation "in.juspay:hypersdk:${getHyperSDKVersion()}"
}
